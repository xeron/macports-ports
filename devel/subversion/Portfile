# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem  1.0

name        subversion


# Subports
set perl5.branches {5.8 5.10 5.12 5.14 5.16}
foreach branch ${perl5.branches} {
    set p_idx [lsearch ${perl5.branches} ${branch}]
    set other_perls [lreplace ${perl5.branches} $p_idx $p_idx]
    subport subversion-perlbindings-${branch} {
        set perl5.branch ${branch}
        foreach c_perl ${other_perls} {
            conflicts-append    subversion-perlbindings-${c_perl}
        }
    }
}


# General
version             1.8.1
revision            1
categories          devel
platforms           darwin
license             Apache-2
maintainers         geeklair.net:dluke blair
long_description    Subversion (svn) is a version control system \
                    designed to be as similar to CVS as possible, \
                    while fixing many outstanding problems with CVS.
homepage            http://subversion.apache.org/

switch -regexp ${subport} {
    perl {
        categories-append       perl
        description             Perl ${perl5.branch} bindings for the \
                                Subversion version control system
        long_description-append These bindings provide access to the \
                                Subversion API from Perl ${perl5.branch}.
    }
    default {
        description             A version control system designed to \
                                be a better CVS
    }
}


# Dependencies
depends_lib port:apr \
            port:apr-util \
            port:cyrus-sasl2 \
            port:db46 \
            port:expat \
            port:gettext \
            port:libcomerr \
            port:libiconv \
            port:libmagic \
            port:serf1 \
            port:sqlite3 \
            port:zlib
switch -regexp ${subport} {
    perl    {depends_lib-append port:${name} port:perl${perl5.branch}}
    default {depends_run        port:curl-ca-bundle}
}


# Fetch and checksum
master_sites    apache:subversion
use_bzip2       yes
checksums       sha1    7705819a0037c14fb32eef36f2e57a803217c689 \
                rmd160  aaff1efd8e1b8112603eb7855cb546316fe88707 \
                sha256  faaaaedba25777331e761884598af1dd9fe33631d6415b2e0ba5348867c4edb4


# Patch
switch -regexp ${subport} {
    perl    {patchfiles patch-swig-perl-native-core.c.diff}
    default {
        patchfiles      config_impl.h.patch
        post-patch {
            reinplace "s|__PREFIX__|${prefix}|" \
                ${worksrcpath}/subversion/libsvn_subr/config_impl.h
        }
    }
}


# Configure
configure.args  --disable-keychain \
                --with-apr=${prefix}/bin/apr-1-config \
                --with-apr-util=${prefix}/bin/apu-1-config \
                --with-berkeley-db=:${prefix}/include/db46:${prefix}/lib/db46:db-4.6 \
                --with-libmagic=${prefix} \
                --with-sasl=${prefix} \
                --with-serf=${prefix} \
                --with-zlib=${prefix} \
                --without-apxs \
                --without-gnome-keyring
switch -regexp ${subport} {
    perl    {configure.args-append  PERL=${prefix}/bin/perl${perl5.branch}}
}
pre-configure {
    reinplace "s|hardcode_direct=yes|hardcode_direct=no|g" \
        ${worksrcpath}/configure
}
post-configure {
    reinplace "s|need_relink=yes|need_relink=no|g" ${worksrcpath}/libtool
}


# Build
switch -regexp ${subport} {
    perl {
        use_parallel_build          no
        build.target                swig-pl
    }
    default {build.target-append    tools}
}


# Destroot
switch -regexp ${subport} {
    perl {
        destroot {
            system -W ${worksrcpath} \
                "${build.cmd} install-swig-pl-lib ${destroot.post_args}"
            system -W ${worksrcpath}/subversion/bindings/swig/perl/native \
                "${build.cmd} pure_install INSTALLDIRS=vendor ${destroot.post_args}"
            fs-traverse pl ${destroot} {
                if {[file tail ${pl}] eq {.packlist}} {
                    ui_info "Fixing packlist ${pl}"
                    reinplace "s|${destroot}||" ${pl}
                }
            }
        }
    }
    default {
        destroot.target-append install-tools
        post-destroot {
            # install global config file so curl-ca-bundle certs are used
            xinstall -d ${destroot}${prefix}/etc/subversion
            xinstall -m 644 ${filespath}/servers.default \
                ${destroot}${prefix}/etc/subversion
            # install bash completion file
            set completions_path \
                ${destroot}${prefix}/share/bash-completion/completions
            xinstall -d ${completions_path}
            xinstall -m 644 ${worksrcpath}/tools/client-side/bash_completion \
                ${completions_path}/subversion
        }
    }
}


# Activate
subport ${name} {
    post-activate {
        if {![file exists ${prefix}/etc/subversion/servers]} {
            copy ${prefix}/etc/subversion/servers.default \
                ${prefix}/etc/subversion/servers
        }
    }
}


# Test
test.run    yes
switch -regexp ${subport} {
    perl    {test.target    check-swig-pl}
    default {
        test.target         check
        test.env            CLEANUP=true
        pre-test {
            set x {}
            fs-traverse dir ${worksrcpath}/subversion {
                if {[file tail ${dir}] == ".libs" && [file isdirectory ${dir}]} {
                    lappend x ${dir}
                    continue
                }
            }
            test.env-append DYLD_LIBRARY_PATH=[join ${x} ":"]
        }
    }
}


# Variants

variant no_bdb description {Build without support for BerkeleyDB repositories} {
    depends_lib-delete      port:db46
    configure.args-replace  --with-berkeley-db=:${prefix}/include/db46:${prefix}/lib/db46:db-4.6 \
                            --without-berkeley-db
}

subport ${name} {
    variant mod_dav_svn description {Install the subversion apache module (mod_dav_svn)} {
        depends_build           path:apache2/bin/apxs:apache2
        configure.args-replace  --without-apxs \
                                --with-apxs=${prefix}/apache2/bin/apxs
        configure.args-append   --disable-mod-activation
        destroot.violate_mtree  yes
    }

    variant mac_os_x_server_mod_dav_svn description {Unsupported - attempt to build the subversion apache module with apple supplied apache2} {
        configure.args-replace  --without-apxs \
                                --with-apxs=/opt/apache2/bin/apxs
        configure.args-append   --disable-mod-activation
        destroot.violate_mtree  yes
        post-install {
            ui_warn "This variant (+mac_os_x_server_mod_dav_svn) builds against the Apple-supplied apache2 in /opt/apache2 and thus may have problems that the normal variant (+mod_dav_svn) which builds against the macports supplied apache2 will not have."
        }
    }

    variant tools description {Install some optional extra subversion tools} {
        post-destroot {
            xinstall -d -m 755 ${destroot}${prefix}/share/${name}
            eval delete ${worksrcpath}/tools/diff/ \
                ${worksrcpath}/tools/server-side/mod_dontdothat \
                [glob ${worksrcpath}/tools/server-side/*{.o,.lo,.c}] \
                ${worksrcpath}/tools/server-side/fsfs-stats \
                ${worksrcpath}/tools/server-side/svn-populate-node-origins-index \
                ${worksrcpath}/tools/server-side/svn-rep-sharing-stats \
                ${worksrcpath}/tools/server-side/svnauthz-validate
            copy ${worksrcpath}/tools ${destroot}${prefix}/share/${name}/tools
        }
    }

    # see http://subversion.tigris.org/issues/show_bug.cgi?id=2464
    variant unicode_path description {Installs a hack to workaround Mac OS X unicode path issues} {
        patchfiles-append   patch-osx_unicode_precomp.diff
        post-install {
            ui_warn "This variant (+unicode_path) implements a hack to deal with composed/decomposed unicode handling on Mac OS X which is different from linux and windows. It is an implementation of solution 1 from http://svn.collab.net/repos/svn/trunk/notes/unicode-composition-for-filenames which _WILL_ break some setups. Please be sure you understand what you are asking for when you install this variant."
        }
    }

    platform macosx {
        # Legacy negative variant to be removed after August 2014.
        variant disable_keychain conflicts osxkeychain description {Legacy compatibility variant} {}
        variant osxkeychain conflicts disable_keychain description {Enable OS X Keychain support} {
            configure.args-delete   --disable-keychain
        }
        if {[variant_isset disable_keychain]} {
            default_variants        -osxkeychain
        } else {
            default_variants        +osxkeychain
        }
    }
}


# Livecheck
if {${subport} eq ${name}} {
    livecheck.type  regex
    livecheck.url   http://svn.apache.org/repos/asf/subversion/tags/
    livecheck.regex {(\d+\.\d+\.\d+)/}
} else {
    livecheck.type  none
}
